apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.2"
    defaultConfig {
        def buildNumber = System.getenv("BUILD_NUMBER") ? (System.getenv("BUILD_NUMBER") as Integer) : 9999

        applicationId "de.keineantwort.android.excludefromgallery"
        minSdkVersion 21
        targetSdkVersion 26

        versionCode buildNumber
        versionName "${buildNumber}"
        logger.info("Buildnumber: $buildNumber, versionCode: $versionCode")

        setProperty("archivesBaseName", "imzp-$versionName-$versionCode")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        def Properties props = new Properties()
        props.load(new FileInputStream(file("${System.properties['user.home']}/.android/keystore.properties")))
        assert props['debug.store']
        assert props['release.store']
        debug {
            storeFile file(props['debug.store'])
        }

        release {
            storeFile file(props['release.store'])
            storePassword props['release.storePass']
            keyAlias props['release.keyAlias']
            keyPassword props['release.keyPass']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:26.0.0-beta2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation ('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
